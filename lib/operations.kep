/**
 * @fileOverview
 */
package (
    executeContext,
    execute,
    
// Evaluation
    evaluateAst,
    evaluateInput,

// Operations
    getFrom,
    getValue,
    
// Execution Context
    context,
    location,
    stack)
with
    import 'ecma/parse/parser' {parse},
    import 'atum/compute' {always},
    import 'atum/operations/internal_reference' internal_reference,
    import 'atum/operations/execution_context' execution_context,
    import 'atum/operations/value_reference' value_reference,
    import 'atum/semantics/semantics' {programBody, sourceElements},
    import 'sheut/run' {debug},
    import 'sheut/step' {finish}
in {

/* Execute
 ******************************************************************************/
/**
 * Evaluate `p` in provide debug context.
 * 
 * @param d Debug context.
 * @param p Computation.
 * @param [ok] Success completion.
 * @param [err] Failure completion.
 */
executeContext = \d, p, ok, err ->
    finish(
        debug(
            p,
            d.ctx,
            ok,
            err)).debug.k;

/**
 * Evaluate `p` in the current context for `d`.
 * 
 * @param d Debugger state.
 * @param p Computation.
 * @param [ok] Success completion.
 * @param [err] Failure completion.
 */
execute = \d, p, ok, err ->
    executeContext(
        d.debug,
        p,
        ok,
        err);

/* Evaluation
 ******************************************************************************/
/**
 * Evaluate an abstract syntax tree in the compute context from `d`.
 */
evaluateAst = \root ->
    programBody(
        sourceElements(root.body));

/**
 * Evaluate a string in the compute context from `d`.
 * 
 * @see evaluate
 */
evaluateInput = parse \> evaluateAst;

/* Operations
 ******************************************************************************/
/**
 * Dereference the result of `p` in the compute context from `d`.
 * 
 * @see evaluate
 */
getFrom = internal_reference.getFrom \> value_reference.getFrom;

/**
 * Dereference `ref` in the compute context from `d`.
 * 
 * @see evaluate
 */
getValue = always \> getFrom;

/* State
 ******************************************************************************/
var _extractMetadata = \f ->
    execution_context.extract(\ctx ->
        f(ctx.metadata));

/**
 * Get the execution context.
 */
context = execution_context.context;

/**
 * Get the current location.
 */
location = _extractMetadata(\m -> m.loc);

/**
 * Get the current stack.
 */
stack = _extractMetadata(\m -> m.stack);

}