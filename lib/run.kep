/**
 * @fileOverview
 */
package (
    execute,
    evaluate,
    extract,
    transform)
with
    import 'atum/compute/statement' statement,
    import 'atum/compute/program' program,
    
    import 'sheut/debug' {debug, 'execute': executeProgram},
    import 'sheut/state' {getResult}
in {

var ret = \x -> x;
var thr = \x -> { throw x; };

var finish = \args(...) -> {
    with
        import 'sheut/step' {finish}
    in {
        return finish.apply(null, args);
    }
};


/**
 * Run `p` in provide debug context.
 * 
 * Calls continuations with the resulting debugger state and result of computation.
 * 
 * @param d Debugger state.
 * @param p Computation.
 * @param ok Success continuation.
 * @param err Failure continuation.
 */
execute = \d, p, ok, err -> let
    result = finish(
        debug(
            d,
            program.liftStatement(statement.liftExpression(p)),
            d.debug.ctx,
            \x, ctx -> \d -> ok(x, ctx, d),
            \x, ctx -> \d -> err(x, ctx, d)))
in
    getResult(result)(result);

/**
 * Evaluate `p` in the given debug context,
 * 
 * @param d Debugger state.
 * @param p Computation,
 * @param [def] Default value to return when `p` fails.
 */
evaluate = \d, p, ok, err ->
    execute(
        d,
        p,
        (ok || ret),
        (err || thr));


/**
 * Evaluate `p` in the given debug context and return the result.
 * 
 * On failure, a default value is returned.
 * 
 * @param d Debugger state.
 * @param p Computation,
 * @param [def] Default value to return when `p` fails.
 * @param [ok] Optional callback to pass the result successful result through before returning it.
 */
extract = \d, p, def, ok -> let
    suc = (ok || ret)
in
    execute(
        d,
        p,
        suc,
        \_, _ -> def);

/**
 * Evaluate `p` in the given debug context and return the next debug context
 * resulting from evaluation.
 * 
 * @param d Debugger state.
 * @param p Computation,
 * @param [ok] Success completion.
 * @param [err] Failure completion.
 */
transform = \d, p, ok, err ->
    finish(
        debug(
            d,
            p,
            d.debug.ctx,
            ok,
            err)).debug;

}